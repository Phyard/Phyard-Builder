<?xml version="1.0" encoding="utf-8"?>
<mx:Application 
         xmlns:mx="http://www.adobe.com/2006/mxml" 
         xmlns:entity="editor.entity.dialog.*"  
         styleName = "plain" 
         layout="absolute" 
         width ="100%"
         height="100%"
         backgroundColor="0x808080"
         borderStyle="solid"
         borderThickness="1"
         usePreloader="true"
         percentWidth="100"
         percentHeight="100"
         scriptTimeLimit="60"
         creationComplete="InitApp ();"
         addedToStage="OnAddedToStage ();"
         enterFrame="UpdateApp ();" 
         mouseDown="EditorContext.GetSingleton ().SetCurrentFocusedTitleWindow (null);"
         >
   
   <mx:Style source="editor.css"/>

   <mx:ApplicationControlBar backgroundColor="0xA0A0A0" dock="true" paddingTop="5" paddingBottom="1" verticalAlign="bottom">

      <mx:HBox id="Box_DesignCommands" verticalAlign="middle" borderStyle="none" height="100%" horizontalGap="1" verticalGap="0" paddingTop="1" paddingLeft="0" paddingRight="0" paddingBottom="0" borderThickness="0">
         <mx:Spacer width="2"/>
         <mx:Button id="Button_LoadWorld" toolTip="Open Design" click="OnClicLoadWorldButton ();" focusEnabled="false" fontSize="8" width="27" height="27" toggle="false" cornerRadius="0" icon="@Embed('../res/file/design-open.png')" />
         <mx:Button id="Button_SaveWorld" toolTip="Save Design" click="OnClicSaveWorldButton ();" focusEnabled="false" fontSize="8" width="27" height="27" toggle="false" cornerRadius="0" icon="@Embed('../res/file/design-save.png')" />
         <mx:Button id="Button_DesignInfo" toolTip="Design Info" click="OnClickConfigWorldButton ();" focusEnabled="false" fontSize="8" width="27" height="27" toggle="false" cornerRadius="0" icon="@Embed('../res/file/design-info.png')" />
      </mx:HBox>
      <mx:Spacer width="100%" />
      <mx:HBox id="Box_SwitchViewCommands" borderStyle="none" height="100%" horizontalGap="0" verticalGap="0" paddingTop="1" paddingLeft="0" paddingRight="0" paddingBottom="1" borderThickness="0">
         <mx:Button id="Button_CollisionCategoryHelp" toolTip="Help" focusEnabled="false" fontSize="8" width="27" height="27" toggle="false" cornerRadius="0" icon="@Embed('../res/editor/editor-about.png')" click="OnClickOpenLinksButton()" />
         <mx:Spacer width="2"/>
      </mx:HBox>
      
   </mx:ApplicationControlBar>

   <mx:XML id="SaveWorldMenuData">
      <root>
         <menuitem label="Save Online ..." data="save_online" for_online="true"/>
         <menuitem label="Save Offline (xml) ..." data="save_offline"/>
         <!--menuitem label="Save Local ..." data="save_local"/-->
         <!--menuitem label="Quick Save (Ctrl + S)" data="save_cookie"/-->
         <menuitem label="Export Selected Entities to System Clipborad" data="export_selected_entities"/>
         <!--menuitem label="Export All Functions to System Clipborad" data="export_all_functions"/-->
      </root>
   </mx:XML>

   <mx:XML id="LoadWorldMenuData">
      <root>
         <menuitem label="Load Online ..." data="load_online" for_online="true"/>
         <menuitem label="Load Offline (xml) ..." data="load_offline"/>
         <!--menuitem label="Load Local ..." data="load_local"/-->
         <!--menuitem label="Quick Load ..." data="load_quick"/-->
         <menuitem label="Import ..." data="import"/>
         <!--menuitem label="Import Functions Only ..." data="import_functions"/-->
      </root>
   </mx:XML>

  <mx:Script><![CDATA[
      
   import flash.system.System;
   
   import flash.events.Event;
   import flash.events.MouseEvent;
   import flash.events.KeyboardEvent;
   import flash.ui.Keyboard;
   import flash.ui.ContextMenu;
   import flash.ui.ContextMenuItem;
   import flash.display.LoaderInfo;
   
   import flash.net.URLRequest;
   import flash.net.URLLoader;
   import flash.net.URLRequestMethod;
   import flash.net.URLLoaderDataFormat;
   
   import mx.containers.VBox;
   import mx.controls.Button;
   import mx.controls.Menu;
   import mx.events.MenuEvent;
   
   import mx.managers.PopUpManager;
   
   import com.tapirgames.util.DisplayObjectUtil;
   import com.tapirgames.util.UrlUtil
   
   import editor.world.World;
   
   import editor.undo.*;
   
   import editor.entity.dialog.SceneEditDialog;
   import editor.entity.dialog.SceneEditPanel;
   import editor.entity.dialog.ScenePlayPanel;
   import editor.entity.Entity;
   import editor.entity.EntityShape;
   
   import editor.ccat.CollisionCategory;
   import editor.ccat.CollisionCategoryManager;
   
   import editor.display.dialog.*;
   import editor.display.sprite.EffectMessagePopup;
   
   import common.DataFormat;
   import common.DataFormat2;
   import common.DataFormat3;
   import common.WorldDefine;
   
   import common.trigger.CoreEventIds;
   import common.ValueAdjuster;
   import common.Define;
   import common.Version;
   
//========================================================================================
// app
//========================================================================================
   
      private function InitApp ():void
      {
         EditorContext.sEditorApp = this;
         
         AddExternalCallbacks ();
      }
      
      private function UpdateApp ():void
      {
         
      }
      
   //================================================================================
   // 
   //================================================================================
      
      private function AddExternalCallbacks ():void
      {         
         if (flash.external.ExternalInterface.available)
         {
             try
             {
                 flash.external.ExternalInterface.addCallback("DesignNotSaved", DesignNotSaved);
             }
             catch (error:Error)
             {
                trace ("Adding external callbacks failed: " + error.message + "\n");
             }
         }
         else
         {
             trace ("External interface is not available for this container.");
         }
      }
            
      private function DesignNotSaved ():Boolean
      {
         return true; // todo
      }
      
   //================================================================================
   // 
   //================================================================================
      
      private var mAppParams:Object = new Object ();
      
      private function OnAddedToStage ():void
      {  
         stage.addEventListener (KeyboardEvent.KEY_DOWN, OnKeyDown);
         stage.addEventListener (MouseEvent.MOUSE_DOWN, OnMouseAction);
         stage.addEventListener (MouseEvent.MOUSE_MOVE, OnMouseAction);
         stage.addEventListener (MouseEvent.MOUSE_UP, OnMouseAction);
         
         mOriginalFPS = stage.frameRate;
         
         // ...
         
         PopUpManager.addPopUp (mSceneEditDialog, this, false);
         PopUpManager.centerPopUp (mSceneEditDialog);
         
         NewWorld ();
         
         // ...
         
         try
         {
            var loadInfo:LoaderInfo = LoaderInfo(stage.root.loaderInfo);
            mAppParams.mRootUrl = UrlUtil.GetRootUrl (loaderInfo.url);
            var flashVars:Object = loaderInfo.parameters;
            if (flashVars != null)
            {
               if (flashVars.action != null)
                  mAppParams.mAction = flashVars.action;
               if (flashVars.author != null)
                  mAppParams.mAuthorName = flashVars.author;
               if (flashVars.slot != null)
                  mAppParams.mSlotID = flashVars.slot;
               if (flashVars.revision != null)
                  mAppParams.mRevisionID = flashVars.revision;
            }
         }
         catch (error:Error)
         {
            trace (error.getStackTrace ());
         }
         
         mIsOnlineEditing = mAppParams.mRootUrl != null && mAppParams.mAction != null && mAppParams.mAuthorName != null && mAppParams.mSlotID != null && mAppParams.mRevisionID != null;
         
         if (IsOnlineEditing () && mAppParams.mAction != "create") // don't change this. If it is changed, old editor versions need rebuilt.
         {
            OnlineLoad (true);
         }
      }
      
      private function OnMouseAction (event:MouseEvent):void
      {
         EditorContext.mIsMouseButtonHold = event.buttonDown;
      }
      
      private var mOriginalFPS:Number = 30;
      
      public function RestoreDefaultFPS ():void
      {
         stage.frameRate = mOriginalFPS;
      }
      
      private var mIsOnlineEditing:Boolean = false;
      
      public function IsOnlineEditing ():Boolean
      {
         return mIsOnlineEditing;
      }
   
//========================================================================================
// world
//========================================================================================
         
      private var mWorld:World = null;

      public function GetWorld ():World
      {
         return mWorld;
      }
      
      public function SetWorld (world:World):void
      {
         CloseWorld ();
         
         EditorContext.sEditorContext = new EditorContext ();
         
         mWorld = world;
         
         mSceneEditDialog.SetScene (mWorld == null ? null : mWorld.GetEntityContainer ());
      }
      
      public function CloseWorld ():void
      {
         mSceneEditDialog.SetScene (null);

         mWorld = null;
         
         if (EditorContext.sEditorContext != null)
            EditorContext.sEditorContext.Cleanup ();
         
         EditorContext.sEditorContext = null;
      }
   
      public function NewWorld ():void
      {
         SetWorld (new World ());
         
         CreateWorldSnapshot ("New world created");
      }
      
      public function LoadWorld ():void
      {
      }
      
      public function SaveWorld ():void
      {
      }
      
   //========================================================================================
   // asset edit dialogs
   //========================================================================================
      
      private function OnKeyDown (event:KeyboardEvent):void
      {
         if (mSceneEditDialog.IsInEditingMode ())
         {
            if (EditorContext.GetSingleton () != null)
            {
               EditorContext.GetSingleton ().OnKeyDownDefault (event.keyCode, event.ctrlKey, event.shiftKey);
            }
         }
      }
      
      private function ShowCollisionCategoryListDialog ():void
      {
         if (EditorContext.GetSingleton () != null)
         {
            EditorContext.GetSingleton ().OnKeyDownDefault (Keyboard.F5);
         }
      }
   
      private function ShowCodeLibListDialog ():void
      {
         if (EditorContext.GetSingleton () != null)
         {
            EditorContext.GetSingleton ().OnKeyDownDefault (Keyboard.F6);
         }
      }
   
      private function ShowAssetImageModuleListDialog ():void
      {
         if (EditorContext.GetSingleton () != null)
         {
            EditorContext.GetSingleton ().OnKeyDownDefault (Keyboard.F3);
         }
      }
   
      private function ShowAssetSoundListDialog ():void
      {
         if (EditorContext.GetSingleton () != null)
         {
            EditorContext.GetSingleton ().OnKeyDownDefault (Keyboard.F4);
         }
      }
      
   //========================================================================================
   // load / save
   //========================================================================================
      
      private var mLoadWorldMenu:Menu = null;
      
      private function OnClicLoadWorldButton ():void
      {
         if (mLoadWorldMenu == null)
         {
            var xml:XML = <root />;
            for each (var menuItem:Object in LoadWorldMenuData.menuitem)
            {
               if (IsOnlineEditing () || menuItem.@["for_online"] != "true")
               {
                  xml.appendChild (menuItem);
               }
            }
            
            mLoadWorldMenu = Menu.createMenu(Button_LoadWorld, xml, false);
            mLoadWorldMenu.labelField="@label";
            mLoadWorldMenu.addEventListener(MenuEvent.ITEM_CLICK, OnSelectLoadWorldMenu);
         }
         
         var point:Point = Button_LoadWorld.localToGlobal (new Point (0, Button_LoadWorld.height));
         
         mLoadWorldMenu.show(point.x, point.y);
      }
      
      private function OnSelectLoadWorldMenu (event:MenuEvent):void
      {
         if (event.item.@data == "load_online")
         {
            OnlineLoad ();
         }
         else if (event.item.@data == "load_offline")
         {
            OnStartOfflineLoading ();
         }
         //else if (event.item.@data == "load_local")
         //{
         //}
         //else if (event.item.@data == "load_quick")
         //{
         //   MainView.QuickLoad ();
         //}
         else if (event.item.@data == "import")
         {
            OnStartOfflineImporting ();
         }
         //else if (event.item.@data == "import_functions")
         //{
         //   MainView.OpenImportSourceCodeDialog (true);
         //}
      }
      
      private var mSaveWorldMenu:Menu = null;
      
      private function OnClicSaveWorldButton ():void
      {
         if (mSaveWorldMenu == null)
         {
            var xml:XML = <root />;
            for each (var menuItem:Object in SaveWorldMenuData.menuitem)
            {
               if (IsOnlineEditing () || menuItem.@["for_online"] != "true")
               {
                  xml.appendChild (menuItem);
               }
            }
            
            mSaveWorldMenu = Menu.createMenu(Button_SaveWorld, xml, false);
            mSaveWorldMenu.labelField="@label";
            mSaveWorldMenu.addEventListener(MenuEvent.ITEM_CLICK, OnSelectSaveWorldMenu);
         }
         
         var point:Point = Button_SaveWorld.localToGlobal (new Point (0, Button_SaveWorld.height));
         
         mSaveWorldMenu.show(point.x, point.y);
      }
      
      private function OnSelectSaveWorldMenu (event:MenuEvent):void
      {
         if (event.item.@data == "save_online")
         {
            OnStartOnlineSaving ();
         }
         else if (event.item.@data == "save_offline")
         {
            OnStartOfflineSaving ();
         }
         //else if (event.item.@data == "save_local")
         //{
         //}
         //else if (event.item.@data == "save_cookie")
         //{
         //   MainView.QuickSave ();
         //}
         else if (event.item.@data == "export_selected_entities")
         {
            OnStartOfflineExporting ();
         }
         //else if (event.item.@data == "export_all_functions")
         //{
         //   MainView.ExportSelectedsToSystemMemory ();
         //}
      }
      
      private function OnStartOfflineLoading ():void
      {
         EditorContext.ShowModalDialog (WorldLoadingDialog, OfflineLoad);
      }
      
      private function OnStartOfflineImporting ():void
      {
         EditorContext.ShowModalDialog (WorldImportDialog, OfflineImport);
      }
      
      public function OnStartOnlineSaving ():void
      {
         EditorContext.ShowModalDialog (WorldOnlineSavingDialog, OnlineSave);
      }
      
      public function OnStartOfflineSaving ():void
      {
         var offlineSaveData:Object = GetOfflineSaveData ();
         if (offlineSaveData != null)
         {
            EditorContext.ShowModalDialog (WorldSavingDialog, null, offlineSaveData);
         }
      }
      
      public function OnStartOfflineExporting ():void
      {
         OfflineExport ();
      }
      
      private function OnClickConfigWorldButton ():void
      {
         //EditorContext.ShowModalDialog (DesignInfoSettingDialog, SetWorldSettings, GetWorldSettings ());
         EditorContext.ShowModalDialog (WorldInfoSettingDialog, SetWorldSettings, GetWorldSettings ());
         
         // holy shit! Flex 3.5 mxmlc compiler will throw many 
         // "Error: 调用可能未定义的方法 GetWorld (通过 static 类型 editor:Editor 引用)",
         // when WorldInfoSettingDialog is named DesignInfoSettingDialog.
      }
      
      private function OnClickOpenLinksButton ():void
      {
         UrlUtil.PopupPage ("http://wiki.colorinfection.com");
      }
      
   //========================================================================================
   // world settings
   //========================================================================================
      
      public function GetWorldSettings ():Object
      {
         var info:Object = new Object ();
         
         info.mShareSoureCode = mWorld.IsShareSourceCode ();
         info.mPermitPublishing = mWorld.IsPermitPublishing ();
         info.mAuthorName = mWorld.GetAuthorName ();
         info.mAuthorHomepage = mWorld.GetAuthorHomepage ();
         
         return info;
      }
      
      public function SetWorldSettings (info:Object):void
      {
         mWorld.SetShareSourceCode (info.mShareSoureCode);
         mWorld.SetPermitPublishing (info.mPermitPublishing);
         mWorld.SetAuthorName (info.mAuthorName);
         mWorld.SetAuthorHomepage (info.mAuthorHomepage);
         
         CreateWorldSnapshot ("Author info is modified");
      }
      
   //========================================================================================
   // online io
   //========================================================================================
      
      // return: online or not
      public function OnlineLoad (isFirstTime:Boolean = false):void
      {
         if (mOnlineLoadingPopup != null)
            return;
         
         var params:Object = mAppParams;
         
         if (params.mRootUrl == null || params.mAction == null || params.mAuthorName == null || params.mSlotID == null || params.mRevisionID == null)
            return;
         
         EditorContext.GetSingleton ().SetRecommandDesignFilename (params.mAuthorName + "-" + params.mSlotID + "-" + params.mRevisionID + ".phyardx", true);
         
         var designLoadUrl:String;
         if (isFirstTime)
         {
            designLoadUrl = params.mRootUrl + "design/" + params.mAuthorName + "/" + params.mSlotID + "/revision/" + params.mRevisionID + "/loadsc";
         }
         else
         {
            designLoadUrl = params.mRootUrl + "design/" + params.mAuthorName + "/" + params.mSlotID + "/revision/latest/loadsc";
            //var isNameRevision:Boolean = isNaN (parseInt (params.mRevisionID)); // "latest", "published"
            //if (isNameRevision)
            //{
               designLoadUrl = designLoadUrl + "?time=" + (new Date ().getTime ()); // avoid browser cache
            //}
         }
         var request:URLRequest = new URLRequest (designLoadUrl);
         request.method = URLRequestMethod.GET;
         
         //trace ("designLoadUrl = " + designLoadUrl);
         
         var loader:URLLoader = new URLLoader ();
         loader.dataFormat = URLLoaderDataFormat.BINARY;
         
         loader.addEventListener (ProgressEvent.PROGRESS, OnOnlineLoadProgress);
         
         loader.addEventListener (SecurityErrorEvent.SECURITY_ERROR, OnOnlineLoadError);
         loader.addEventListener (IOErrorEvent.IO_ERROR, OnOnlineLoadError);
         
         loader.addEventListener(Event.COMPLETE, OnOnlineLoadCompleted);
         
         loader.load ( request );
         
         mOnlineLoadingPopup = new EffectMessagePopup ("Loading ...", EffectMessagePopup.kBgColor_Special, 0x000000, 0.5 * mSceneEditDialog.GetSceneEditPanel ().GetPanelWidth ());
         mOnlineLoadingPopup.SetAutoFade (false);
         mSceneEditDialog.GetSceneEditPanel ().PushFloatingMessage (mOnlineLoadingPopup);
      }
      
      private var mOnlineLoadingPopup:EffectMessagePopup = null;
      
      private function OnOnlineLoadProgress (event:ProgressEvent):void
      {
         if (mOnlineLoadingPopup != null)
            mOnlineLoadingPopup.Rebuild ("Loading ... (" + Math.floor (100 * event.bytesLoaded / event.bytesTotal) + "%)", EffectMessagePopup.kBgColor_Special, 0x000000);
      } 
      
      private function OnOnlineLoadError (event:Event):void
      {
         if (mOnlineLoadingPopup != null)
            mOnlineLoadingPopup.SetAutoFade (true);
         
         mOnlineLoadingPopup = null;
         
         mSceneEditDialog.GetSceneEditPanel ().PushFloatingMessage (new EffectMessagePopup ("Online load error", EffectMessagePopup.kBgColor_Error, 0x000000, 0.5 * mSceneEditDialog.GetSceneEditPanel ().GetPanelWidth ()));
      } 
      
      private function OnOnlineLoadCompleted(event:Event):void 
      {
         if (mOnlineLoadingPopup != null)
            mOnlineLoadingPopup.SetAutoFade (true);
         
         mOnlineLoadingPopup = null;
         
         var loader:URLLoader = URLLoader(event.target);
         
         var returnCode:int = Define.k_ReturnCode_UnknowError;
         
         var data:ByteArray = ByteArray (loader.data);
         
         returnCode = data.readByte ();
         
         if (returnCode != Define.k_ReturnCode_Successed)
         {
            //Alert.show("Some errors in loading! returnCode = " + returnCode, "Error");
            mSceneEditDialog.GetSceneEditPanel ().PushFloatingMessage (new EffectMessagePopup ("Online load error,  returnCode = " + returnCode, EffectMessagePopup.kBgColor_Error, 0x000000, 0.5 * mSceneEditDialog.GetSceneEditPanel ().GetPanelWidth ()));
         }
         else
         {
            CloseWorld ();
            
            try
            {  
               var designDataForEditing:ByteArray = new ByteArray ();
               
               data.readBytes (designDataForEditing);
               designDataForEditing.uncompress ();
                  
               var newWorld:World = new World ();
               SetWorld (newWorld);
               DataFormat.WorldDefine2EditorWorld (true, newWorld, DataFormat2.ByteArray2WorldDefine (designDataForEditing));
               
               CreateWorldSnapshot ("Online loading is done");
               
               //Alert.show("Loading Succeeded!", "Succeeded");
               mSceneEditDialog.GetSceneEditPanel ().PushFloatingMessage (new EffectMessagePopup ("Online load succeeded", EffectMessagePopup.kBgColor_OK, 0x000000, 0.5 * mSceneEditDialog.GetSceneEditPanel ().GetPanelWidth ()));
            }
            catch (error:Error)
            {
               if (Capabilities.isDebugger)
                  throw error;
               
               RestoreWorld (mWorldHistoryManager.GetCurrentWorldState ());
               
               //Alert.show("Sorry, online loading error!", "Error");
               
               mSceneEditDialog.GetSceneEditPanel ().PushFloatingMessage (new EffectMessagePopup ("Online load error", EffectMessagePopup.kBgColor_Error, 0x000000, 0.5 * mSceneEditDialog.GetSceneEditPanel ().GetPanelWidth ()));
            }
         }
      }
      
      public function OnlineSave (options:Object = null):void
      {
         //
         var isImportant:Boolean = false;
         var revisionComment:String = "";
         if (options != null)
         {
            isImportant = options.mIsImportant;
            revisionComment = options.mRevisionComment.substr (0, 100);
         }
         var designDataRevisionComment:ByteArray = new ByteArray ();
         //designDataRevisionComment.writeMultiByte (revisionComment, "utf-8"); // has bug on linux
         designDataRevisionComment.writeUTFBytes (revisionComment);
         designDataRevisionComment.position = 0;
         
         //
         var params:Object = mAppParams;
         
         if (params.mRootUrl == null || params.mAuthorName == null || params.mSlotID == null)
            return;
         
         var designDataForEditing:ByteArray = DataFormat.WorldDefine2ByteArray (DataFormat.EditorWorld2WorldDefine (mWorld));
         designDataForEditing.compress ();
         designDataForEditing.position = 0;
         
         var designDataForPlaying:ByteArray = DataFormat.WorldDefine2ByteArray (DataFormat.EditorWorld2WorldDefine (mWorld));
         designDataForPlaying.compress ();
         designDataForPlaying.position = 0;
         
         var designDataAll:ByteArray = new ByteArray ();
         
         designDataAll.writeInt (Version.VersionNumber);
         
         //>> from v1.07 (in fact, the code added in v0110 r003)
         var shareSourceCode:Boolean = mWorld.IsShareSourceCode ();
         designDataAll.writeShort (mWorld.GetEntityContainer ().GetViewportWidth ()); // view width
         designDataAll.writeShort (mWorld.GetEntityContainer ().GetViewportHeight ()); // view height
         //designDataAll.writeByte  ((mWorld.GetEntityContainer ().GetViewerUiFlags () & Define.PlayerUiFlag_ShowPlayBar) != 0 ? 1 : 0); // show play bar?
         designDataAll.writeByte  ((mWorld.GetEntityContainer ().GetViewerUiFlags () & (Define.PlayerUiFlag_UseDefaultSkin | Define.PlayerUiFlag_UseOverlaySkin)) == Define.PlayerUiFlag_UseDefaultSkin ? 1 : 0);
         designDataAll.writeByte  (shareSourceCode ? 1 : 0); // share source code?
         //<<
         
         //
         
         designDataAll.writeByte (isImportant ? 1 : 0);
         
         designDataAll.writeInt (designDataRevisionComment.length);
         designDataAll.writeInt (designDataForEditing.length);
         //designDataAll.writeInt (shareSourceCode ? 0 : designDataForPlaying.length);
         designDataAll.writeInt (0); // !! currently, playing data is totally same as editing data
         
         designDataAll.writeBytes (designDataRevisionComment);
         designDataAll.writeBytes (designDataForEditing);
         if (! shareSourceCode)
         {
            //designDataAll.writeBytes (designDataForPlaying); // !! currently, playing data is totally same as editing data
         }
         
         var designSaveUrl:String = params.mRootUrl + "design/" + params.mAuthorName + "/" + params.mSlotID + "/save";
         var request:URLRequest = new URLRequest (designSaveUrl);
         request.contentType = "application/octet-stream";
         request.method = URLRequestMethod.POST;
         request.data = designDataAll;
         
         //trace ("designSaveUrl = " + designSaveUrl)
         
         var loader:URLLoader = new URLLoader ();
         loader.dataFormat = URLLoaderDataFormat.BINARY;
            
         //loader.addEventListener (ProgressEvent.PROGRESS, OnOnlineSaveProgress); // will not be triggered
         
         loader.addEventListener (SecurityErrorEvent.SECURITY_ERROR, OnOnlineSaveError);
         loader.addEventListener (IOErrorEvent.IO_ERROR, OnOnlineSaveError);

         loader.addEventListener(Event.COMPLETE, OnOnlineSaveCompleted);
         
         loader.load ( request );
         //navigateToURL ( request )
         
         mOnlineSavingPopup = new EffectMessagePopup ("Saving ... (Please don't close the editor!)", EffectMessagePopup.kBgColor_Special, 0x000000, 0.5 * mSceneEditDialog.GetSceneEditPanel ().GetPanelWidth ());
         mOnlineSavingPopup.SetAutoFade (false);
         mSceneEditDialog.GetSceneEditPanel ().PushFloatingMessage (mOnlineSavingPopup);
      }
      
      private var mOnlineSavingPopup:EffectMessagePopup = null;
      
      private function OnOnlineSaveError (event:Event):void
      {
         if (mOnlineSavingPopup != null)
            mOnlineSavingPopup.SetAutoFade (true);
         
         mSceneEditDialog.GetSceneEditPanel ().PushFloatingMessage (new EffectMessagePopup ("Online save error", EffectMessagePopup.kBgColor_Error, 0x000000, 0.5 * mSceneEditDialog.GetSceneEditPanel ().GetPanelWidth ()));
      } 
      
      private function OnOnlineSaveCompleted(event:Event):void 
      {
         if (mOnlineSavingPopup != null)
            mOnlineSavingPopup.SetAutoFade (true);
         
         var loader:URLLoader = URLLoader(event.target);
         
         try
         {
            var data:ByteArray = ByteArray (loader.data);
            
            var returnCode:int = data.readByte ();
            var returnMessage:String = null;
            if (data.length > data.position)
            {
               var length:int = data.readInt ();
               returnMessage = data.readUTFBytes (length);
            }
            
            if (returnCode == Define.k_ReturnCode_Successed)
            {
               mSceneEditDialog.GetSceneEditPanel ().PushFloatingMessage (new EffectMessagePopup ("Online save succeeded", EffectMessagePopup.kBgColor_OK, 0x000000, 0.5 * mSceneEditDialog.GetSceneEditPanel ().GetPanelWidth ()));
            }
            else
            {
               //Alert.show("Some errors in saving! returnCode = " + returnCode + ", returnMessage = " + returnMessage, "Error");
               var errorMessage:String = "Online save failed,  returnCode = " + returnCode + ",  returnMessage = " + returnMessage;
               mSceneEditDialog.GetSceneEditPanel ().PushFloatingMessage (new EffectMessagePopup (errorMessage, EffectMessagePopup.kBgColor_Error, 0x000000, 0.5 * mSceneEditDialog.GetSceneEditPanel ().GetPanelWidth ()));
            }
         }
         catch (error:Error)
         {
            if (Capabilities.isDebugger)
               throw error;
            
            //Alert.show("Sorry, online saving error! " + loader.data + " " + error, "Error");
            
            RestoreWorld (mWorldHistoryManager.GetCurrentWorldState ());
            
            mSceneEditDialog.GetSceneEditPanel ().PushFloatingMessage (new EffectMessagePopup ("Online save error", EffectMessagePopup.kBgColor_Error, 0x000000, 0.5 * mSceneEditDialog.GetSceneEditPanel ().GetPanelWidth ()));
         }
      }
      
   //========================================================================================
   // offline io
   //========================================================================================
      
      // load 
      public function OfflineLoad (params:Object):void
      {
         CloseWorld ();
         
         try
         {
            var codeString:String = params.mXmlString;
            
            var newWorldDefine:WorldDefine = null;
            
            if (codeString != null)
            {
               const Text_PlayCode:String = "playcode";
               if (codeString.length > Text_PlayCode.length && codeString.substring (0, Text_PlayCode.length) == Text_PlayCode)
               {
                  var Text_OldCodeStarting:String = "434F494E";
                  var offset:int = codeString.indexOf (Text_OldCodeStarting, Text_PlayCode.length);
                  if (offset > 0) // old playcode
                  {
                     newWorldDefine = DataFormat2.HexString2WorldDefine (codeString.substring (offset));
                  }
                  else // new base64 playcode
                  {
                     offset = Text_PlayCode.length;
                     var Text_CompressFormat:String = "compressformat=base64";
                     offset = codeString.indexOf (Text_CompressFormat, offset);
                     if (offset > 0)
                     {
                        offset += Text_CompressFormat.length;
                        var Text_PlayCode2:String = "playcode=";
                        offset = codeString.indexOf (Text_PlayCode2, offset);
                        if (offset > 0)
                        {
                           offset += Text_PlayCode2.length;
                           var offset2:int = codeString.indexOf ("@}", offset);
                           if (offset2 > 0)
                           {
                              newWorldDefine = DataFormat2.PlayCode2WorldDefine_Base64 (codeString.substring (offset, offset2));
                           }
                        }
                     }
                  }
               }
               else
               {
                  var xml:XML = new XML (codeString);
                  
                  newWorldDefine = DataFormat.Xml2WorldDefine (xml);
               }
            }
            
            if (newWorldDefine == null)
               throw new Error ("newWorldDefine == null !!!");
            
            var newWorld:World = new World ();
            SetWorld (newWorld);
            DataFormat.WorldDefine2EditorWorld (true, newWorld, newWorldDefine);
            
            //mWorldHistoryManager.ClearHistories ();
            
            mSceneEditDialog.GetSceneEditPanel ().PushFloatingMessage (new EffectMessagePopup ("Offline loading succeeded", EffectMessagePopup.kBgColor_OK, 0x000000, 0.5 * mSceneEditDialog.GetSceneEditPanel ().GetPanelWidth ()));
            
            CreateWorldSnapshot ("Offline loading is done");
         }
         catch (error:Error)
         {
            if (Capabilities.isDebugger)
               throw error;
            
            //Alert.show ("Loading error: " + error + "\n " + error.getStackTrace ());
            
            RestoreWorld (mWorldHistoryManager.GetCurrentWorldState ());
            
            mSceneEditDialog.GetSceneEditPanel ().PushFloatingMessage (new EffectMessagePopup ("Offline loading failed", EffectMessagePopup.kBgColor_Error, 0x000000, 0.5 * mSceneEditDialog.GetSceneEditPanel ().GetPanelWidth ()));
         }
      }
      
      public function OfflineImport (params:Object):void
      {
         ImportFromXmlString (params, true, true);
      }
      
      private function ImportFromXmlString (params:Object, importEntities:Boolean, importFunctions:Boolean):void
      {
         var xmlString:String = params.mXmlString;
         var mergeVariablesWithSameNames:Boolean = params.mMergeVariablesWithSameNames;
         var centerNewEntitiesInScreen:Boolean = params.mCenterNewEntitiesInScreen;         
         
         var xml:XML = new XML (xmlString);
         
         try
         {
            var oldEntitiesCount:int = mWorld.GetEntityContainer ().GetNumAssets ();
            var oldCategoriesCount:int = mWorld.GetCollisionCategoryManager ().GetNumCollisionCategories ();
            
            var worldDefine:WorldDefine = DataFormat.Xml2WorldDefine (xml);
            
            if (oldEntitiesCount + worldDefine.mEntityDefines.length > Define.MaxEntitiesCount)
               return;
            
            if (oldCategoriesCount + worldDefine.mCollisionCategoryDefines.length > Define.MaxCCatsCount)
               return;
            
            DataFormat.WorldDefine2EditorWorld (false, mWorld, worldDefine, true, mergeVariablesWithSameNames);
            
            if (mWorld.GetCodeLibManager ().IsChanged ())
            {
               mWorld.GetCodeLibManager ().UpdateFunctionMenu ();
               mWorld.GetCodeLibManager ().SetChanged (false);
            }
            
            if (oldEntitiesCount == mWorld.GetEntityContainer ().GetNumAssets ())
               return;
            
            mWorld.GetEntityContainer ().CancelAllAssetSelections ();
            
            var i:int;
            var j:int;
            var entity:Entity;
            var entities:Array;
            var centerX:Number = 0;
            var centerY:Number = 0;
            var numSelecteds:int = 0;
            
            var newEntitiesCount:int = mWorld.GetEntityContainer ().GetNumAssets ();
            
            var entitiesToSelect:Array = new Array ();
            for (i = oldEntitiesCount; i < newEntitiesCount; ++ i)
            {
               entities = (mWorld.GetEntityContainer ().GetAssetByCreationId (i) as Entity).GetSelectableAssets ();
               
               for (j = 0; j < entities.length; ++ j)
               {
                  entity = entities [j] as Entity;
                  
                  if (entitiesToSelect.indexOf (entity) < 0)
                  {
                     entitiesToSelect.push(entity);
                     
                     centerX += entity.GetPositionX ();
                     centerY += entity.GetPositionY ();
                     ++ numSelecteds;
                  }
               }
            }
            
            mWorld.GetEntityContainer ().SetSelectedAssets (entitiesToSelect);

            if (centerNewEntitiesInScreen && numSelecteds > 0)
            {
               centerX /= numSelecteds;
               centerY /= numSelecteds;
               
               var point:Point = mSceneEditDialog.GetSceneEditPanel ().GetPanelCenterWorldPoint ();
               mSceneEditDialog.GetSceneEditPanel ().MoveSelectedAssets (false, point.x - centerX, point.y - centerY, true);
            }
            
            mSceneEditDialog.GetSceneEditPanel ().PushFloatingMessage (new EffectMessagePopup ("Import succeeded", EffectMessagePopup.kBgColor_OK, 0x000000, 0.5 * mSceneEditDialog.GetSceneEditPanel ().GetPanelWidth ()));
            
            CreateWorldSnapshot ("Import");
         }
         catch (error:Error)
         {
            if (Capabilities.isDebugger)
               throw error;
            
            //Alert.show("Sorry, import error!", "Error");
            
            RestoreWorld (mWorldHistoryManager.GetCurrentWorldState ());
            
            mSceneEditDialog.GetSceneEditPanel ().PushFloatingMessage (new EffectMessagePopup ("Import failed", EffectMessagePopup.kBgColor_Error, 0x000000, 0.5 * mSceneEditDialog.GetSceneEditPanel ().GetPanelWidth ()));
         }
      }
      
      public function GetOfflineSaveData ():Object
      {
         try
         {
            // ...
            var width:int = mWorld.GetEntityContainer ().GetViewportWidth ();
            var height:int = mWorld.GetEntityContainer ().GetViewportHeight ();
            //var showPlayBar:Boolean = (mWorld.GetEntityContainer ().GetViewerUiFlags () & Define.PlayerUiFlag_ShowPlayBar) != 0;
            var showPlayBar:Boolean = (mWorld.GetEntityContainer ().GetViewerUiFlags () & (Define.PlayerUiFlag_UseDefaultSkin | Define.PlayerUiFlag_UseOverlaySkin)) == Define.PlayerUiFlag_UseDefaultSkin;
            var heightWithPlayBar:int = (showPlayBar ? height + Define.DefaultPlayerSkinPlayBarHeight : height);
            
            var fileFormatVersionString:String = DataFormat3.GetVersionHexString (Version.VersionNumber);
            
            var values:Object = new Object ();
            
            //========== source code ========== 
         
            values.mXmlString = DataFormat2.WorldDefine2Xml (DataFormat.EditorWorld2WorldDefine (mWorld));
            
            //=========== play code ========== 
         
            // before v1.55, depreciated now
            //var playcode:String = DataFormat.WorldDefine2HexString (DataFormat.EditorWorld2WorldDefine (mWorld)); 
            //values.mHexString = playcode;
            
            // new one, from v1.55
            var playcodeBase64:String = DataFormat.WorldDefine2PlayCode_Base64 (DataFormat.EditorWorld2WorldDefine (mWorld));
            
            values.mHexString = DataFormat3.CreateForumEmbedCode (fileFormatVersionString, width, height, showPlayBar, playcodeBase64);
            
            //======== html embed code ======== 
         
            // before v1.55, depreciated now
            //values.mEmbedCode = "<embed src=\"http://www.phyard.com/uniplayer.swf?app=ci&format=0x" + fileFormatVersionString
            //                  + "\"\n width=\"" + width + "\" height=\"" + heightWithPlayBar + "\"\n"
            //                  + "  FlashVars=\"playcode=" + playcode
            //                  + "\"\n quality=\"high\" allowScriptAccess=\"sameDomain\"\n type=\"application/x-shockwave-flash\"\n pluginspage=\"http://www.macromedia.com/go/getflashplayer\">\n</embed>"
            //                  ;
            
            // new one: add compressformat in FlashVars; change app=ci to app=coin and change format to fileversion in url.
            values.mEmbedCode = "<embed src=\"http://www.phyard.com/uniplayer.swf?app=coin&fileversion=0x" + fileFormatVersionString
                              + "\"\n width=\"" + width + "\" height=\"" + heightWithPlayBar + "\"\n"
                              + "  FlashVars=\"compressformat=" + DataFormat3.CompressFormat_Base64 + "&playcode=" + playcodeBase64
                              + "\"\n quality=\"high\" allowScriptAccess=\"sameDomain\"\n type=\"application/x-shockwave-flash\"\n pluginspage=\"http://www.macromedia.com/go/getflashplayer\">\n</embed>"
                              ;
            
            return values;
         }
         catch (error:Error)
         {
            //Alert.show("Sorry, saving error!", "Error");
            
            if (Capabilities.isDebugger)
               throw error;
         }
         
         return null;
      }
      
      public function OfflineExport ():void
      {
         try
         {
            var worldDefine:WorldDefine = DataFormat.EditorWorld2WorldDefine (mWorld);
            var newWorld:World = new World ();
            newWorld = DataFormat.WorldDefine2EditorWorld (true, newWorld, worldDefine);
               // !!! here may be some bugs: somewhere will call EditorContext.GetCurrentWorld, which is not the newWorld
            
            var selectedEntities:Array = mWorld.GetEntityContainer ().GetSelectedAssets ();
            
            if (selectedEntities.length == 0)
               return;
            
            var numEntities:int = selectedEntities.length;
            var i:int;
            var index:int;
            var entity:Entity;
            for (i = 0; i < numEntities; ++ i)
            {
               //trace ("selectedEntities[i] = " + selectedEntities[i]);
               //trace ("selectedEntities[i].parent = " + selectedEntities[i].parent);
               
               index = mWorld.GetEntityContainer ().GetAssetCreationId (selectedEntities[i]);
               entity = newWorld.GetEntityContainer ().GetAssetByCreationId (index) as Entity;
               
               entity = entity.GetMainAsset () as Entity;
               newWorld.GetEntityContainer ().AddAssetSelection (entity);
               newWorld.GetEntityContainer ().AddAssetSelections (entity.GetSelectableAssets ());
            }
            
            i = 0;
            //numEntities = newWorld.GetEntityContainer ().GetNumAssets (); // this is bug
            while (i < newWorld.GetEntityContainer ().GetNumAssets ()) // numEntities)
            {
               entity = newWorld.GetEntityContainer ().GetAssetByCreationId (i) as Entity;
               if ( entity.IsSelected () ) // generally should use world.IsEntitySelected instead, this one is fast but only for internal uses
               {
                  ++ i;
               }
               else
               {
                  newWorld.GetEntityContainer ().DestroyAsset (entity);
               }
            }
            
            //var cm:CollisionCManager = newWorld.GetCollisionManager ();
            //var ccId:int;
            //numEntities = newWorld.GetEntityContainer ().GetNumAssets ();
            //
            //for (i = 0; i < numEntities; ++ i)
            //{
            //   entity = newWorld.GetEntityContainer ().GetAssetByCreationId (i) as Entity;
            //   if (entity is EntityVectorShape)
            //   {
            //      ccId = (entity as EntityVectorShape).GetCollisionCategoryIndex ();
            //      if (ccId >= 0)
            //         cm.SelectEntity (cm.GetCollisionCategoryByIndex (ccId));
            //   }
            //}
            //
            //ccId = 0;
            // //var numCats:int = cm.GetNumCollisionCategories ();// this is bug
            //while (ccId < cm.GetNumCollisionCategories ()) //numCats)
            //{
            //   entity = cm.GetCollisionCategoryByIndex (ccId);
            //   if ( entity.IsSelected () ) // generally should use world.IsEntitySelected instead, this one is fast but only for internal uses
            //   {
            //      ++ ccId;
            //   }
            //   else
            //   {
            //      cm.DestroyEntity (entity);
            //   }
            //}
            
            var cm:CollisionCategoryManager = newWorld.GetCollisionCategoryManager ();
            cm.CancelAllAssetSelections ();
            var ccId:int;
            numEntities = newWorld.GetEntityContainer ().GetNumAssets ();
            
            for (i = 0; i < numEntities; ++ i)
            {
               entity = newWorld.GetEntityContainer ().GetAssetByCreationId (i) as Entity;
               if (entity is EntityShape)
               {
                  ccId = (entity as EntityShape).GetCollisionCategoryIndex ();
                  if (ccId >= 0)
                     cm.AddAssetSelection (cm.GetCollisionCategoryByIndex (ccId));
               }
            }
            
            var ccat:CollisionCategory;
            ccId = 0;
            //var numCats:int = cm.GetNumCollisionCategories ();// this is bug
            while (ccId < cm.GetNumCollisionCategories ()) //numCats)
            {
               ccat = cm.GetCollisionCategoryByIndex (ccId);
               if ( ccat.IsSelected () ) // generally should use world.IsEntitySelected instead, this one is fast but only for internal uses
               {
                  ++ ccId;
               }
               else
               {
                  cm.DestroyAsset (ccat);
               }
            }
            
            System.setClipboard(DataFormat2.WorldDefine2Xml (DataFormat.EditorWorld2WorldDefine (newWorld)));
            
            mSceneEditDialog.GetSceneEditPanel ().PushFloatingMessage (new EffectMessagePopup ("Export succeeded", EffectMessagePopup.kBgColor_OK, 0x000000, 0.5 * mSceneEditDialog.GetSceneEditPanel ().GetPanelWidth ()));
         }
         catch (error:Error)
         {
            //Alert.show("Sorry, export  error!", "Error");
            
            mSceneEditDialog.GetSceneEditPanel ().PushFloatingMessage (new EffectMessagePopup ("Export failed", EffectMessagePopup.kBgColor_Error, 0x000000, 0.5 * mSceneEditDialog.GetSceneEditPanel ().GetPanelWidth ()));
            
            if (Capabilities.isDebugger)
               throw error;
         }
         //finally // comment off for bug of secureSWF 
         //{
            if (newWorld != null)
               newWorld.Destroy ();
         //}
      }
      
   //========================================================================================
   // undo / redo
   //========================================================================================
      
      private var mWorldHistoryManager:WorldHistoryManager = new WorldHistoryManager ();
      
      public function GetWorldSnapshotManager ():WorldHistoryManager
      {
         return mWorldHistoryManager;
      }
      
      public function CreateWorldSnapshot (description:String, editActions:Array = null):void
      {
         if (mWorld == null)
            return;
         
         var worldState:WorldState = new WorldState (description, editActions);
         
         var object:Object = new Object ();
         worldState.mUserData = object;
         
         object.mWorldDefine = DataFormat.EditorWorld2WorldDefine (mWorld);
         
         var entityArray:Array = mWorld.GetEntityContainer ().GetSelectedAssets ();
         
         object.mSelectedEntityCreationIds = new Array (entityArray.length);
         object.mMainSelectedEntityId = -1;
         object.mSelectedVertexControllerId = -1;
         
         for (var i:int = 0; i < entityArray.length; ++ i)
         {
            var entity:Entity = entityArray [i] as Entity;
            var entityId:int = mWorld.GetEntityContainer ().GetAssetCreationId (entity);
            object.mSelectedEntityCreationIds [i] = entityId;
            if (entity.AreControlPointsVisible ())
            {
               object.mMainSelectedEntityId = entityId;
               
               //var vertexControllerArray:Array = mWorld.GetEntityContainer ().GetSelectedVertexControllers ();
               //if (vertexControllerArray.length > 0)
               //   object.mSelectedVertexControllerId = entity.GetVertexControllerIndex (vertexControllerArray [0]);
            }
         }
         
         mWorldHistoryManager.AddHistory (worldState);
         
         mSceneEditDialog.GetSceneEditPanel ().PushFloatingMessage (new EffectMessagePopup ("Undo point created (" + description + ")", EffectMessagePopup.kBgColor_General, 0x000000, 0.5 * mSceneEditDialog.GetSceneEditPanel ().GetPanelWidth ()));
      }
      
      private function RestoreWorld (worldState:WorldState):void
      {
         if (worldState == null)
         {
            //OnCloseClearAllAndResetSceneAlert (null);
            return;
         }
         
         CloseWorld ();
         
         try
         {  
            var currentSceneX:Number = mSceneEditDialog.GetSceneEditPanel ().GetCurrentManagerX ();
            var currentSceneY:Number = mSceneEditDialog.GetSceneEditPanel ().GetCurrentManagerY ();
            var currentSceneScale:Number = mSceneEditDialog.GetSceneEditPanel ().GetCurrentManagerScale ();
            
            mSceneEditDialog.GetSceneEditPanel ().CancelAllAssetSelections ();
            
            var object:Object = worldState.mUserData;
            
            var newWorld:World = new World ();
            SetWorld (newWorld);
            DataFormat.WorldDefine2EditorWorld (true, newWorld, object.mWorldDefine, false);
            
            mSceneEditDialog.GetSceneEditPanel ().MoveManagerTo (currentSceneX, currentSceneY);
            mSceneEditDialog.GetSceneEditPanel ().ScaleManagerTo (currentSceneScale);
            
            mWorld.GetEntityContainer ().SetZoomScale (mSceneEditDialog.GetSceneEditPanel ().GetCurrentManagerScale ());
            
            var numEntities:int = mWorld.GetEntityContainer ().GetNumAssets ();
            var entityId:int;
            var entity:Entity;
            
            for (var i:int = 0; i < object.mSelectedEntityCreationIds.length; ++ i)
            {
               entityId = object.mSelectedEntityCreationIds [i];
               if (entityId >= 0 && entityId < numEntities)
               {
                  entity = mWorld.GetEntityContainer ().GetAssetByCreationId (entityId) as Entity;
                  mWorld.GetEntityContainer ().AddAssetSelection (entity);
                  
                  if (entityId == object.mMainSelectedEntityId)
                  {
                     entity.SetControlPointsVisible (true);
                     
                     //if (object.mSelectedVertexControllerId >= 0)
                     //{
                     //   var vertexController:VertexController = entity.GetVertexControllerByIndex (object.mSelectedVertexControllerId);
                     //   if (vertexController != null)
                     //      mWorld.GetEntityContainer ().SetSelectedVertexController (vertexController);
                     //}
                  }
               }
            }
            
            mSceneEditDialog.GetSceneEditPanel ().OnAssetSelectionsChanged ();
         }
         catch (error:Error)
         {
            if (Capabilities.isDebugger)
               throw error;
            
            RestoreWorld (mWorldHistoryManager.GetCurrentWorldState ());
            
            //Alert.show("Sorry, online loading error!", "Error");
            
            mSceneEditDialog.GetSceneEditPanel ().PushFloatingMessage (new EffectMessagePopup ("Online load error", EffectMessagePopup.kBgColor_Error, 0x000000, 0.5 * mSceneEditDialog.GetSceneEditPanel ().GetPanelWidth ()));
         }
      }
      
      public function Undo ():void
      {
         var worldState:WorldState = mWorldHistoryManager.UndoHistory ();
         
         if (worldState == null)
         {
            mSceneEditDialog.GetSceneEditPanel ().PushFloatingMessage (new EffectMessagePopup ("No undo points available", EffectMessagePopup.kBgColor_General, 0x000000, 0.5 * mSceneEditDialog.GetSceneEditPanel ().GetPanelWidth ()));
            return;
         }
         
         RestoreWorld (worldState);
         
         worldState = worldState.GetNextWorldState ();
         if (worldState != null) // should not
         {
            mSceneEditDialog.GetSceneEditPanel ().PushFloatingMessage (new EffectMessagePopup ("Undo (" + worldState.GetDescription () + ")", EffectMessagePopup.kBgColor_OK, 0x000000, 0.5 * mSceneEditDialog.GetSceneEditPanel ().GetPanelWidth ()));
         }
      }
      
      public function Redo ():void
      {
         var worldState:WorldState = mWorldHistoryManager.RedoHistory ();
         
         if (worldState == null)
         {
            mSceneEditDialog.GetSceneEditPanel ().PushFloatingMessage (new EffectMessagePopup ("No redo points available", EffectMessagePopup.kBgColor_General, 0x000000, 0.5 * mSceneEditDialog.GetSceneEditPanel ().GetPanelWidth ()));
            return;
         }
         
         RestoreWorld (worldState);
         
         mSceneEditDialog.GetSceneEditPanel ().PushFloatingMessage (new EffectMessagePopup ("Redo (" + worldState.GetDescription () + ")", EffectMessagePopup.kBgColor_OK, 0x000000, 0.5 * mSceneEditDialog.GetSceneEditPanel ().GetPanelWidth ()));
      }
      
   //========================================================================================
   // 
   //========================================================================================
      
      public function GetWorldBinaryData ():ByteArray
      {
         if (mWorld == null)
            return null;
         
         var byteArray:ByteArray = DataFormat.WorldDefine2ByteArray (DataFormat.EditorWorld2WorldDefine (mWorld));
         byteArray.position = 0;
         
         return byteArray;
      }
      
//========================================================================================
// !!!
//========================================================================================
      
      import player.WorldPlugin;
      
      private function Dummy ():void
      {
         new WorldPlugin (); // to make mxmlc include WorldPlugin, so the all player.* package, ...
      }
      
//========================================================================================
// scene edit dialog
//========================================================================================
      
      private var mSceneEditDialog:SceneEditDialog = new SceneEditDialog ();
      
      public function GetSceneEditDialog ():SceneEditDialog
      {
         return mSceneEditDialog;
      }

   ]]></mx:Script>
</mx:Application>

